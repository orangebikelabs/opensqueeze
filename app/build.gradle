/*
 * Copyright (c) 2020-2022 The OpenSqueeze Authors. All Rights Reserved.
 * Use of this source code is governed by the license that can be found in the LICENSE file.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'app.cash.sqldelight'

android {
    namespace 'com.orangebikelabs.orangesqueeze'

    compileSdk 35

    sourceSets {
        main {
            java {
                srcDir 'src/main/sqldelight'
            }
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE', 'META-INF/services/com.fasterxml.jackson.core.JsonFactory', 'META-INF/MANIFEST.MF', 'META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }


    defaultConfig {
        applicationId 'org.opensqueeze'

        versionCode obl_android_app_versionCode
        versionName obl_android_app_versionName

        // android >= 5.0
        minSdkVersion 21
        targetSdkVersion 34
        resourceConfigurations += ['en', 'de', 'fr', 'sv', 'zh']


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('keystores/opensqueeze_release.jks')
            storePassword "opensqueeze"
            keyAlias "android"
            keyPassword "opensqueeze"
        }
        debug {
            storeFile file('keystores/opensqueeze_debug.jks')
            storePassword "opensqueeze"
            keyAlias "android"
            keyPassword "opensqueeze"
        }
    }

    buildTypes {
        release {
            minifyEnabled true

            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-project.pro'
            proguardFile 'proguard-guava.pro'
            proguardFile 'proguard-jackson.pro'
            proguardFile 'proguard-otto.pro'

            signingConfig signingConfigs.release
        }

        debug {
            multiDexEnabled true

            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    lint {
        abortOnError true
    }

    // override the password if we supply it
    if (project.hasProperty('releaseKeystorePassword')) {
        signingConfigs.release.storePassword = releaseKeystorePassword
        signingConfigs.release.keyPassword = releaseKeystorePassword
    }
    if (project.hasProperty('debugKeystorePassword')) {
        signingConfigs.debug.storePassword = debugKeystorePassword
        signingConfigs.debug.keyPassword = debugKeystorePassword
    }
}

sqldelight {
    databases {
        // Database name
        OSDatabase {
            // generated package for main database
            packageName = "com.orangebikelabs.orangesqueeze.database"

            // The directory where to store '.db' schema files relative to the root
            // of the project. These files are used to verify that migrations yield
            // a database with the latest schema. Defaults to null so the verification
            // tasks will not be created.
            schemaOutputDirectory = file("src/main/databases")
        }
    }
}

dependencies {

    // must stay at 3.12.x until minsdk >= 25
    def okhttpVersion = '3.12.13'
    def jsr305Version = '3.0.2'

    // must stay at 2.12.7 for sdk 21
    def jacksonVersion = '2.12.7'
    def materialdialogsVersion = '3.3.0'


    // java8 desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1'

    // lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'

    // recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    // ListViewAnimations
    implementation 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    implementation 'com.nhaarman.listviewanimations:lib-manipulation:3.1.0@aar'

    implementation "com.afollestad.material-dialogs:core:${materialdialogsVersion}"
    implementation "com.afollestad.material-dialogs:input:${materialdialogsVersion}"
    implementation "com.afollestad.material-dialogs:datetime:${materialdialogsVersion}"
    implementation "com.afollestad.material-dialogs:files:${materialdialogsVersion}"
    implementation "com.afollestad.material-dialogs:lifecycle:${materialdialogsVersion}"
    implementation "com.afollestad.material-dialogs:bottomsheets:${materialdialogsVersion}"

    // NineOldAndroids for ListViewAnimations
    implementation 'com.nineoldandroids:library:2.4.0'

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.8'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.media:media:1.7.0'

    implementation 'com.google.android.material:material:1.12.0'

    //noinspection GradleDependency
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    //noinspection GradleDependency
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    //noinspection GradleDependency
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    //noinspection GradleDependency
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-smile:${jacksonVersion}"

    implementation "com.google.guava:guava:33.4.0-android"

    // HTTP client
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
    implementation 'com.squareup.okio:okio:3.10.2'

    // permissions
    implementation 'com.github.fondesa:kpermissions:3.5.0'

    // event bus
    implementation 'com.squareup:otto:1.3.8'

    // annotations
    implementation "com.google.code.findbugs:jsr305:${jsr305Version}"

    // ReactiveX
    implementation "io.reactivex.rxjava3:rxandroid:3.0.2"
    implementation "io.reactivex.rxjava3:rxjava:3.1.10"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"

    // sqldelight
    implementation "app.cash.sqldelight:android-driver:${sqldelight_version}"
    implementation "app.cash.sqldelight:coroutines-extensions:${sqldelight_version}"
    implementation "app.cash.sqldelight:primitive-adapters:${sqldelight_version}"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation fileTree(dir: 'lib', include: ['*.jar'])

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk-android:1.14.2"
    testImplementation "com.google.truth:truth:1.4.4"

    androidTestImplementation "androidx.test:runner:1.6.2"
    androidTestImplementation "androidx.test:rules:1.6.1"
    androidTestImplementation "androidx.test:core:1.6.1"
    androidTestImplementation "androidx.test:core-ktx:1.6.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.6.1"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.2.1"
    androidTestImplementation "androidx.test.ext:truth:1.6.0"
    androidTestImplementation "io.mockk:mockk-android:1.14.2"
    androidTestUtil "androidx.test:orchestrator:1.5.1"

    // workaround for gradle bug https://github.com/gradle/gradle/issues/22326, keep until fixed
    modules {
        module("com.google.guava:listenablefuture") {
            replacedBy("com.google.guava:guava", "listenablefuture is part of guava")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    // uncomment this for more detailed deprecation warnings
    //options.compilerArgs << "-Xlint:deprecation"
    //options.compilerArgs << "-Xlint:unchecked"
}

tasks.register('archiveApks') {
    description = "Copies APKs and mapping files to the archive directory"
    doLast {
        def appName = "${rootProject.name}"
        def versionDir = android.defaultConfig.versionName + "-" + android.defaultConfig.versionCode

        copy {
            from fileTree(dir: 'build/outputs/apk').files
            into 'build/archive'
            include '*.apk'
            rename('app-(.*)\\.apk', "${appName}-${versionDir}-\$1.apk")
        }

        copy {
            from 'build/outputs/mapping'
            into 'build/archive/mapping'
        }
    }
}
archiveApks.mustRunAfter assemble

// Test Logging
tasks.withType(Test).configureEach {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}